plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties.setProperty('keystorePath', '__placeholder')
    logger.lifecycle('Could not read keystore.properties file')
}

android {
    namespace = "dev.huynh.gift.box"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21
    }

    defaultConfig {
        applicationId = "dev.huynh.gift.box"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
            storeFile = file('gift_box.jks')
            storePassword = keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters 'arm64-v8a'
            }
            minifyEnabled true
            shrinkResources true
        }

        debug {
            signingConfig signingConfigs.debug
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                def versionName = variant.versionName
                def variantName = variant.name

                outputFileName = "gift_box_${variantName}_v${versionName}.apk"
            }
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation 'androidx.window:window-core-android:1.5.0'
}
